Teeny Felt Newt

Medium

# It's possible to bypass total supply check under some conditions

### Summary

When tokens are about to swapped, the `ss` struct is always checked to ensure the validity, one of the check is after swap, the total supply will be over limited value. However, under some condition, this can be bypassed, breaking the invariant. 

### Root Cause

[`StablecoinHandler::_verifyStablecoinSwap`](https://github.com/sherlock-audit/2024-11-telcoin/blob/main/telcoin-audit/contracts/stablecoin/StablecoinHandler.sol#L194) verifies the validity of `ss`:
```solidity
    function _verifyStablecoinSwap(
        address wallet,
        StablecoinSwap memory ss
    ) internal view nonZero(ss) {
        // Ensure the wallet address is not zero.
        if (wallet == address(0)) revert ZeroValueInput("WALLET");

        // For the origin currency:
        if (isXYZ(ss.origin)) {
            // Ensure the total supply does not drop below the minimum limit after burning the specified amount.
            if (
                Stablecoin(ss.origin).totalSupply() - ss.oAmount <
                getMinLimit(ss.origin)
            ) revert InvalidMintBurnBoundry(ss.origin, ss.oAmount);
        } else if (ss.liquiditySafe == address(0)) {
            // Ensure the liquidity safe is provided for ERC20 origin tokens.
            revert ZeroValueInput("LIQUIDITY SAFE");
        }

        // For the target currency:
        if (isXYZ(ss.target)) {
            // Ensure the total supply does not exceed the maximum limit after minting the specified amount.
            if (
                Stablecoin(ss.target).totalSupply() + ss.tAmount >
                getMaxLimit(ss.target)
            ) revert InvalidMintBurnBoundry(ss.target, ss.tAmount);
        } else if (ss.liquiditySafe == address(0)) {
            // Ensure the liquidity safe is provided for ERC20 target tokens.
            revert ZeroValueInput("LIQUIDITY SAFE");
        }
    }
```

One of the check is to make sure, after swap, the total supply of XYZ token is above min limit. In `AmirX` contract, multiple swap related functions would reassign value of `ss.oAmount` after `_defiSwap`:
```solidity
    function swap(
        address wallet,
        bool directional,
        StablecoinSwap memory ss,
        DefiSwap memory defi
    ) external payable onlyRole(SWAPPER_ROLE) whenNotPaused {
        // checks if it will fail
        if (ss.destination != address(0)) _verifyStablecoinSwap(wallet, ss);
        if (defi.walletData.length != 0) _verifyDefiSwap(wallet, defi);

        if (directional) {
            // if only defi swap
            if (ss.destination == address(0)) _defiSwap(wallet, defi);
            else {
                // if defi then stablecoin swap
                //check balance to adjust second swap
                uint256 iBalance = ERC20(ss.origin).balanceOf(wallet);
                if (defi.walletData.length != 0) _defiSwap(wallet, defi);
                uint256 fBalance = ERC20(ss.origin).balanceOf(wallet);
                //change balance to reflect change
                if (fBalance - iBalance != 0) ss.oAmount = fBalance - iBalance;
                _stablecoinSwap(wallet, ss);
            }
        } else {
            // if stablecoin swap
            _stablecoinSwap(wallet, ss);
            // if only stablecoin swap
            if (defi.walletData.length != 0) _defiSwap(wallet, defi);
        }
    }

    /**
     * @notice performs a defiswap that feeds into a stablecoin swap
     * @dev Validates stablecoin swap parameters, performs swaps, and handles DeFi interactions based on provided DefiSwap details.
     * @param wallet Address initiating the swap.
     * @param ss StablecoinSwap structure with swap details.
     * @param defi DefiSwap structure with DeFi swap details.
     */
    function defiToStablecoinSwap(
        address wallet,
        StablecoinSwap memory ss,
        DefiSwap memory defi
    ) external payable onlyRole(SWAPPER_ROLE) whenNotPaused {
        // checks if defi will fail
        _verifyDefiSwap(wallet, defi);
        // checks if stablecoin swap will fail
        _verifyStablecoinSwap(wallet, ss);

        //check balance to adjust second swap
        uint256 iBalance = ERC20(ss.origin).balanceOf(wallet);
        _defiSwap(wallet, defi);
        uint256 fBalance = ERC20(ss.origin).balanceOf(wallet);
        ss.oAmount = fBalance - iBalance;
        //change balance to reflect change
        _stablecoinSwap(wallet, ss);
    }
```

However, if `fBalance - iBalance > ss.oAmount`, this can make the value of  `Stablecoin(ss.target).totalSupply() - ss.oAmount` less than the previous calculation, in some edge cases, this can be lower than the minimum limit, but there would be no revert, as the previous check is already passed. This is quite unlikely to happen, as it would require the swap to result more tokenOut than expected.

### Internal pre-conditions

The total supply of XYZ token is 1000, and the lower limit is 900.

### External pre-conditions

`ss.origin` is an XYZ token, and the price of the pair fluctuates a bit, causing `fBalance - iBalance` to be more than the original `ss.oAmount`. For example, expected `oAmount` to be 100, this would make total supply after swap 900, barely passing the min limit.

### Attack Path

After swap, total XYZ swapped is 102 due to price fluctuation. The total supply would drop below 900, which is the min limit, breaking the invariant. 

### Impact

Under this extreme edge condition, total supply of XYZ token can drop below min limit, breaking invariant.

### PoC

_No response_

### Mitigation

Do `ss` check after `_defiSwap`.