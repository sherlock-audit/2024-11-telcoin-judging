Passive Mango Rabbit

Medium

# If totalSupply of a stablecoin is less than the minimum limit, the swap functions will cause DoS

### Summary

If the totalSupply of a stablecoin is less than the minimum limit, or new minimum limit is set to more than the current supply of the stablecoin, the swap functions will revert. This can happen when the stablecoin is new and not many people has minted it initially.

### Root Cause

Minimum supply checks should not be applied when the total supply is already less than minimum supply for swap.

### Internal pre-conditions

The stablecoin is in it's initial stage.

### External pre-conditions

N/A

### Attack Path

1. The users minted 10k stablecoin initially. 
2.  The maintainer sets the total supply to 20k.
3. One of the user tries to swap the stablecoin with another token but the transaction revert due the minimum limit.

### Impact

The user will not be able to swap the tokens as long as the minimum supply is more than the current supply due the following check is `_verifyStablecoinSwap(...)`

```solidity
    function _verifyStablecoinSwap(address wallet, StablecoinSwap memory ss) internal view nonZero(ss) {
        // Ensure the wallet address is not zero.
        if (wallet == address(0)) revert ZeroValueInput("WALLET");

        // For the origin currency:
        if (isXYZ(ss.origin)) {
            // Ensure the total supply does not drop below the minimum limit after burning the specified amount.
  @>          if (Stablecoin(ss.origin).totalSupply() - ss.oAmount < getMinLimit(ss.origin)) {
                revert InvalidMintBurnBoundry(ss.origin, ss.oAmount);
            }
        } else if (ss.liquiditySafe == address(0)) {
            // Ensure the liquidity safe is provided for ERC20 origin tokens.
            revert ZeroValueInput("LIQUIDITY SAFE");
        }

       ....

    }
```

Github: [Link](https://github.com/sherlock-audit/2024-11-telcoin/blob/main/telcoin-audit/contracts/stablecoin/StablecoinHandler.sol#L205)

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.22;

import {Test} from "forge-std/Test.sol";
import {console2} from "forge-std/console2.sol";
import {Stablecoin} from "../../contracts/stablecoin/Stablecoin.sol";
import {StablecoinHandler} from "../../contracts/stablecoin/StablecoinHandler.sol";
import {AmirX} from "../../contracts/swap/AmirX.sol";
import {TestPlugin} from "../../contracts/swap/test/TestPlugin.sol";
import {TestAggregator} from "../../contracts/swap/test/TestAggregator.sol";
import {TestTokenDynamic} from "../../contracts/test/TestTokenDynamic.sol";
import {TestWallet} from "../../contracts/test/TestWallet.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {ISimplePlugin} from "../../contracts/swap/interfaces/ISimplePlugin.sol";
import {TransferFeeToken} from "@weird-erc20/TransferFee.sol";

contract FoTtoken is TransferFeeToken {
    constructor(uint256 _totalSupply, uint256 _fee) TransferFeeToken(_totalSupply, _fee) {}

    function mintTo(address to, uint256 amount) public {
        balanceOf[to] += amount;
        totalSupply += amount;
    }

    function mint(address to, uint256 amount) public {
        balanceOf[to] += amount;
        totalSupply += amount;
    }
}

contract MyTestWallet {
    Stablecoin public eUSD;
    Stablecoin public eEUR;
    ERC20 public telcoin;

    constructor(Stablecoin _eEUR, Stablecoin _eUSD, ERC20 _telcoin) {
        eUSD = _eUSD;
        eEUR = _eEUR;
        telcoin = _telcoin;
    }

    function performSwap(
        MyTestAggregator aggregator,
        address fromToken,
        address toToken,
        address to,
        uint256 fromAmount,
        uint256 toAmount
    ) public {
        // approve the aggregator for tokens
        ERC20(fromToken).approve(address(aggregator), fromAmount);

        // approve AmirX for the target tokens
        uint256 oldAllowance = IERC20(toToken).allowance(address(this), msg.sender);
        ERC20(toToken).approve(msg.sender, toAmount + oldAllowance);

        // perform the swap
        aggregator.swap(fromToken, toToken, to, fromAmount, toAmount);
    }

    fallback() external payable {}
    receive() external payable {}
}

contract MyTestAggregator {
    Stablecoin public eUSD;
    Stablecoin public eEUR;
    ERC20 public telcoin;

    constructor(Stablecoin _eEUR, Stablecoin _eUSD, ERC20 _telcoin) {
        eUSD = _eUSD;
        eEUR = _eEUR;
        telcoin = _telcoin;
    }

    function swap(address fromToken, address toToken, address to, uint256 fromAmount, uint256 toAmount) public {
        if (toToken == address(eUSD)) {
            // transfer the toTokens
            eUSD.transfer(to, toAmount);

            // take in the fromTokens
            ERC20(fromToken).transferFrom(msg.sender, address(this), fromAmount);
        } else if (toToken == address(eEUR)) {
            // transfer the toTokens
            eEUR.transfer(to, toAmount);

            // take in the fromTokens
            ERC20(fromToken).transferFrom(msg.sender, address(this), fromAmount);
        } else {
            // transfer the toTokens
            ERC20(toToken).transfer(to, toAmount);

            // take in the fromTokens
            ERC20(fromToken).transferFrom(msg.sender, address(this), fromAmount);
        }
    }

    fallback() external payable {}
    receive() external payable {}
}

contract MyTest is Test {
    struct Protocol {
        Stablecoin eUSD;
        Stablecoin eEUR;
        StablecoinHandler stablecoinHandler;
        AmirX amirX;
        TestPlugin testPlugin;
        MyTestAggregator testAggregator;
    }

    struct Tokens {
        TestTokenDynamic usdc;
        TestTokenDynamic dai;
        TestTokenDynamic telcoin;
        FoTtoken usdt;
    }

    struct Users {
        address alice;
        address bob;
        address protocolAdmin;
        // Stablecoin roles
        address blacklistAdmin;
        address support;
        // AmirX roles
        address pauser;
        address swapper;
        address maintainer;
    }

    Protocol public protocol;
    Tokens public tokens;
    Users public users;

    function _changePrank(address user) public {
        vm.stopPrank();
        vm.startPrank(user);
    }

    function setUp() public {
        // deploy users
        users.alice = makeAddr("alice");
        users.bob = makeAddr("bob");
        users.protocolAdmin = makeAddr("protocolAdmin");
        users.blacklistAdmin = makeAddr("blacklistAdmin");
        users.support = makeAddr("support");
        users.pauser = makeAddr("pauser");
        users.swapper = makeAddr("swapper");
        users.maintainer = makeAddr("maintainer");

        // deploy tokens
        _changePrank(users.protocolAdmin);
        tokens.usdc = new TestTokenDynamic("USDC", "USDC", 6, users.protocolAdmin, 100000e6);
        tokens.dai = new TestTokenDynamic("DAI", "DAI", 18, users.protocolAdmin, 100000e18);
        TestTokenDynamic telcoin = new TestTokenDynamic("Telcoin", "TEL", 18, users.protocolAdmin, 100000e18);
        vm.etch(0xdF7837DE1F2Fa4631D716CF2502f8b230F1dcc32, address(telcoin).code);
        tokens.telcoin = TestTokenDynamic(payable(0xdF7837DE1F2Fa4631D716CF2502f8b230F1dcc32));
        tokens.usdt = new FoTtoken(100000e18, 2);

        // deploy protocol contracts
        protocol.amirX = new AmirX();
        protocol.eUSD = new Stablecoin();
        protocol.eEUR = new Stablecoin();
        protocol.testAggregator = new MyTestAggregator(protocol.eEUR, protocol.eUSD, ERC20(address(tokens.telcoin)));
        protocol.testPlugin = new TestPlugin(ERC20(address(tokens.telcoin)));

        // initialize the protocol contracts
        protocol.amirX.initialize();
        protocol.eUSD.initialize("USD Stable Coin", "USD", 18);
        protocol.eEUR.initialize("EUR Stable Coin", "EUR", 18);

        // grant roles for eUSD
        assert(protocol.eUSD.hasRole(protocol.eUSD.DEFAULT_ADMIN_ROLE(), users.protocolAdmin));
        protocol.eUSD.grantRole(protocol.eUSD.BURNER_ROLE(), address(protocol.amirX));
        protocol.eUSD.grantRole(protocol.eUSD.MINTER_ROLE(), address(protocol.amirX));
        protocol.eUSD.grantRole(protocol.eUSD.MINTER_ROLE(), address(users.protocolAdmin));
        protocol.eUSD.grantRole(protocol.eUSD.BLACKLISTER_ROLE(), users.blacklistAdmin);
        protocol.eUSD.grantRole(protocol.eUSD.SUPPORT_ROLE(), users.support);
        // grant roles for eEUR
        assert(protocol.eEUR.hasRole(protocol.eEUR.DEFAULT_ADMIN_ROLE(), users.protocolAdmin));
        protocol.eEUR.grantRole(protocol.eEUR.BURNER_ROLE(), address(protocol.amirX));
        protocol.eEUR.grantRole(protocol.eEUR.MINTER_ROLE(), address(protocol.amirX));
        protocol.eEUR.grantRole(protocol.eEUR.MINTER_ROLE(), address(users.protocolAdmin));
        protocol.eEUR.grantRole(protocol.eEUR.BLACKLISTER_ROLE(), users.blacklistAdmin);
        protocol.eEUR.grantRole(protocol.eEUR.SUPPORT_ROLE(), users.support);
        protocol.amirX.grantRole(protocol.amirX.PAUSER_ROLE(), users.pauser);
        protocol.amirX.grantRole(protocol.amirX.SWAPPER_ROLE(), users.swapper);
        protocol.amirX.grantRole(protocol.amirX.MAINTAINER_ROLE(), users.maintainer);
        protocol.amirX.grantRole(protocol.amirX.SUPPORT_ROLE(), users.support);

        // update teh eXYZ in amirX
        // min and max limits to be set to highest and the lowest possible values
        _changePrank(users.maintainer);
        protocol.amirX.UpdateXYZ(address(protocol.eEUR), true, type(uint256).max, 0);
        protocol.amirX.UpdateXYZ(address(protocol.eUSD), true, type(uint256).max, 0);

        // label the contracts
        vm.label(address(protocol.amirX), "AmirX");
        vm.label(address(protocol.eUSD), "eUSD");
        vm.label(address(protocol.eEUR), "eEUR");
        vm.label(address(protocol.testAggregator), "TestAggregator");
        vm.label(address(protocol.testPlugin), "TestPlugin");
        vm.label(address(tokens.usdc), "USDC");
        vm.label(address(tokens.dai), "DAI");
        vm.label(address(tokens.telcoin), "Telcoin");

        // mint every token to aggregator
        _changePrank(users.protocolAdmin);
        // protocol.eUSD.mintTo(address(protocol.testAggregator), 1000e18);
        tokens.usdc.mintTo(address(protocol.testAggregator), 1000e18);
        protocol.eEUR.mintTo(address(protocol.testAggregator), 1000e18);
        tokens.dai.mintTo(address(protocol.testAggregator), 1000e18);
        tokens.telcoin.mintTo(address(protocol.testAggregator), 1000e18);
        vm.deal(address(protocol.testAggregator), 1000e18);
    }

    function test_whenTotalSupplyIsLessThanTheMinLimitTheSwapWillCauseDoS() public {
        // create new wallet
        MyTestWallet wallet = new MyTestWallet(protocol.eEUR, protocol.eUSD, ERC20(address(tokens.telcoin)));

        // prepare the stablecoin swap
        StablecoinHandler.StablecoinSwap memory ss = StablecoinHandler.StablecoinSwap({
            liquiditySafe: users.protocolAdmin,
            destination: users.alice,
            origin: address(protocol.eUSD),
            oAmount: 10,
            target: address(protocol.eEUR),
            tAmount: 20,
            stablecoinFeeCurrency: address(0),
            stablecoinFeeSafe: address(0),
            feeAmount: 0
        });

        // protcol updates the minimum limits
        _changePrank(users.maintainer);
        protocol.amirX.UpdateXYZ(address(protocol.eEUR), true, type(uint256).max, 100);

        // mint some tokens to wallet
        tokens.usdc.mintTo(address(wallet), 10);

        // do the swap
        _changePrank(users.swapper);
        protocol.amirX.stablecoinSwap(address(wallet), ss);
    }
}

```

```bash
   ├─ [0] VM::startPrank(swapper: [0x4A9D6b0b19CBFfCB0255550661eCB7014283c60E])
    │   └─ ← [Return] 
    ├─ [16174] AmirX::stablecoinSwap(MyTestWallet: [0x49861AED287cEDC227f16F8dF03d6E860E293D1F], StablecoinSwap({ liquiditySafe: 0x2eFCD805EB7C816bF3b3F6427cb6B7514Ed0DF87, destination: 0x328809Bc894f92807417D2dAD6b7C998c1aFdac6, origin: 0x948906bB4877B66830377ff95d9F35A0920cEE6B, oAmount: 10, target: 0x594937C7D745f682519fbC93FE9bEC79078da4bC, tAmount: 20, stablecoinFeeCurrency: 0x0000000000000000000000000000000000000000, stablecoinFeeSafe: 0x0000000000000000000000000000000000000000, feeAmount: 0 }))
    │   ├─ [2349] eUSD::totalSupply() [staticcall]
    │   │   └─ ← [Return] 0
    │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
    └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)

Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 3.95ms (269.11µs CPU time)

Ran 1 test suite in 2.30s (3.95ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/MyTests/MyTest.sol:MyTest
[FAIL: panic: arithmetic underflow or overflow (0x11)] test_checking2() (gas: 410835)

Encountered a total of 1 failing tests, 0 tests succeeded

```

### Mitigation

Do not make the minimum supply check if the current supply is already less than the minimum. Or try to find some other way.